# Version 2.0 - minimalism
# Assume installed (via brew): nvim, coreutils 

export EDITOR=nvim
export XDG_CONFIG_HOME=$HOME/.config
export XDG_CACHE_HOME=$HOME/.cache
export XDG_DATA_HOME=$HOME/.local/share
export ZDOTDIR=$XDG_CONFIG_HOME/zsh
export NVIMDIR=$XDG_CONFIG_HOME/nvim

mkdir -p $XDG_CONFIG_HOME       \
         $XDG_CACHE_HOME        \
         $XDG_DATA_HOME         \
         $ZDOTDIR               \
         $NVIMDIR

setopt prompt_subst
setopt hash_list_all
setopt completeinword
setopt menu_complete

zstyle :compinstall filename "$ZDOTDIR/zshrc"

autoload -Uz compinit
compinit

autoload -Uz promptinit
promptinit

autoload -Uz colors 
colors

# virtualenvwrapper
export WORKON_HOME=$XDG_DATA_HOME/virtualenvs
export VIRTUALENVWRAPPER_PYTHON=$(which python3)
source /usr/local/bin/virtualenvwrapper.sh

# Dotfile editing / sourcing
alias vv="$EDITOR $NVIMDIR/init.vim"
alias zv="$EDITOR $ZDOTDIR/zshrc"
alias sv="source $ZDOTDIR/zshrc"

export LSCOLORS='Gxfxexdxcxegedabagacad'                                                                    # BSD (i.e. OS X)
export LS_COLORS='di=1;36:ln=35:so=34:pi=33:ex=32:bd=34;46:cd=34;43:su=30;41:sg=30;46:tw=30;42:ow=30;43'    # Linux / Posix

# Coreutils aliases (brew installed specific)
alias ls="gls -hF --color=auto --group-directories-first "
alias la="ls -la "
alias ll="ls -l "
alias grep="grep --color=auto "

# Man Page Stylin
export MANWIDTH=160
export LESS_TERMCAP_mb=$'\E[01;31m'
export LESS_TERMCAP_md=$'\E[01;31m'
export LESS_TERMCAP_me=$'\E[0m'
export LESS_TERMCAP_se=$'\E[0m'
export LESS_TERMCAP_so=$'\E[01;44;33m'
export LESS_TERMCAP_ue=$'\E[0m'
export LESS_TERMCAP_us=$'\E[01;32m'

# History
HISTSIZE=5000
HISTFILE=$ZDOTDIR/.zsh_history
SAVEHIST=5000
setopt appendhistory
setopt sharehistory
setopt incappendhistory

# Keybindings & Edit Modes
bindkey -v                                                      # Use vi-mode
bindkey '^?' backward-delete-char
bindkey '^h' backward-delete-char
bindkey '^w' backward-kill-word
bindkey 'jk' vi-cmd-mode
bindkey '^[[A'  history-beginning-search-backward               # <UP>
bindkey '^[[B'  history-beginning-search-forward                # <DOWN>
bindkey '^R'	history-incremental-pattern-search-backward     # <C-R>

zmodload -i zsh/parameter
zmodload -i zsh/complist
zmodload -i zsh/deltochar
zmodload -i zsh/mathfunc

# Completion Configuration
zstyle ':completion:*'                      menu select=5
zstyle ':completion:*:default'              list-colors ${(s.:.)LS_COLORS}

# Completion keybindings
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -M menuselect '^[[Z' reverse-menu-complete              # Shift Tab (BackTab)
bindkey -M menuselect '+' accept-and-menu-complete              # <Plus>

# Main Prompt
PS1='%(?..[%F{red}%?%f])'           # If the last command had a non-zero status, display it
PS1+='%B%F{cyan}%n%f%b'             # username
PS1+="@"                            # *nix traditions...
PS1+='%B%F{white}%m%f%b '           # hostname
PS1+='%F{cyan}%20<..<%~%<<%f '      # the CWD truncated to 20 chars
PS1+='[%!]'                         # Current history event number
PS1+='${VIMODE}'                    # ZLE's current edit mode

PS2='\`%_> '                        # secondary prompt, printed when the shell needs more information to complete a command.
PS3='?# '                           # selection prompt used within a select loop.
PS4='+%N:%i:%_> '                   # the execution trace prompt (setopt xtrace). default: '+%N:%i>'

# '$' for normal insert mode
# a big red 'I' for command mode - to me this is 'NOT insert' because red
function zle-line-init zle-keymap-select {
    DOLLAR='%B%F{green}$%f%b '
    GIANT_I='%B%F{red}I%f%b '
    VIMODE="${${KEYMAP/vicmd/$GIANT_I}/(main|viins)/$DOLLAR}"
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

# Other convenience aliases
alias gst="git status"
alias gbv="git branch -vv"
